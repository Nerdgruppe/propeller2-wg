.org 0x0000

pix_driver:
         SETQ    10 - 1                         // get 10 longs from hub
         RDLONG  connect, ptra

         MOV     t1, 0
         WRLONG  t1, ptra                       // tell hub we're connected

rgbx_main:
         RDLONG  connect, ptra :wz               // check for new connection
  if(!Z) JMP     pix_driver

         MOV     addr, p_hub                    // point to rgbbuf[0]
         MOV     npix, pixcount                 // set // active pixels

frame_loop:
         RDLONG  colorbits, addr                // read a channel
         ADD     addr, 4                        // point to next
         TJZ     swapflag, shift_out            // skip fix if swap = 0

        // Correct placement of rg color bytes
        // -- $RR_GG_BB_WW --> $GG_RR_BB_WW

         MOV     t1, colorbits                  // copy
         SHR     t1, 16                         // isolate r & g
         SETBYTE colorbits, t1, 3               // colorbits.byte[3] = g
         SHR     t1, 8
         SETBYTE colorbits, t1, 2               // colorbits.byte[2] = r

shift_out:
         GETCT   bittimer                       // start timing frame

         REP     @.bitz, pixelbits              // loop through all bits
         ROL     colorbits, 1 :wc               // get MSB
         DRVH    tx                             // pin on
  if(!C) WAITX   bit0hi                         // hold for bit timing
  if(C)  WAITX   bit1hi
         DRVL    tx                             // pin off
         ADDCT1  bittimer, cycletix             // update cycle timer
         WAITCT1                                // let cycle finish
.bitz:

next_pixel:        DJNZ    npix, frame_loop     // done with all leds?

reset_delay:        GETCT   bittimer            // reset delay
         ADDCT1  bittimer, resettix
         WAITCT1

         JMP     rgbx_main                      // back to top

// --------------------------------------------------------------------------------------------------

var connect:    .RES    1
var p_hub:      .RES    1
var pixcount:   .RES    1
var tx:         .RES    1
var pixelbits:  .RES    1
var resettix:   .RES    1
var swapflag:   .RES    1
var bit0hi:     .RES    1
var bit1hi:     .RES    1
var cycletix:   .RES    1

var addr:       .RES    1
var npix:       .RES    1
var colorbits:  .RES    1

var bittimer:   .RES    1

var t1:         .RES    1
var t2:         .RES    1
var t3:         .RES    1

.fit    496
